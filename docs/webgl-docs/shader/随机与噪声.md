# éšæœºä¸å™ªå£°
ä¹‹å‰ç»˜åˆ¶çš„å¤§éƒ¨åˆ†å›¾æ¡ˆéƒ½æ˜¯æœ‰åºçš„ï¼Œè§„å¾‹çš„ï¼Œä½†å…¶å®ç°å®çš„å¤§éƒ¨åˆ†æ™¯è±¡éƒ½æ˜¯æ— åºå’Œæ··ä¹±çš„ã€‚

ç”±äºShaderæ˜¯å’Œæ•°å­¦ç´§å¯†ç›¸å…³çš„ï¼Œè€Œåœ¨æ•°å­¦ä¸­ï¼Œè·Ÿæ— åºä¸æ··æ²Œæœ€æ¥è¿‘çš„ä¸€ä¸ªæ¦‚å¿µå°±æ˜¯**éšæœº**ã€‚

## å¯åŠ¨æ¨¡ç‰ˆ
æ–°å»ºä¸€ä¸ª`.glsl`æ–‡ä»¶ï¼Œåˆå§‹åŒ–ä»£ç å¦‚ä¸‹ï¼š
```glsl
void mainImage(out vec4 fragColor,in vec2 fragCoord){
    vec2 uv=fragCoord/iResolution.xy;
    fragColor=vec4(uv,0.,1.);
}
```
## éšæœºå‡½æ•°
åœ¨`JS`ä¸­ï¼Œå¯ä»¥æœ‰å†…ç½®çš„éšæœºå‡½æ•°`Math.random`æ¥æ¨¡æ‹Ÿéšæœºï¼Œä½†æ˜¯`GLSL`å´æ²¡æœ‰å†…ç½®çš„éšæœºå‡½æ•°å¯ä»¥ä½¿ç”¨ï¼Œä½†æ˜¯åœ¨`GitHub`ä¸Šï¼Œå·²ç»æœ‰äººå°è£…å¥½äº†éšæœºå‡½æ•°[glsl-random](https://github.com/mattdesl/glsl-random)ï¼Œæ¥çœ‹çœ‹é•¿ä»€ä¹ˆæ ·å­ï¼š

```glsl
highp float random(vec2 co)
{
    highp float a=12.9898;
    highp float b=78.233;
    highp float c=43758.5453;
    highp float dt=dot(co.xy,vec2(a,b));
    highp float sn=mod(dt,3.14);
    return fract(sin(sn)*c);
}
```
è¿™ä¸ªéšæœºå‡½æ•°çš„æ„ä¹‰æ˜¯å–`sin`å‡½æ•°ååé¢çš„å°æ•°éƒ¨åˆ†æ¥æ¨¡æ‹Ÿéšæœºçš„æ•ˆæœï¼Œä¼ å…¥uvåæ ‡ï¼Œå¯ä»¥å¾—å‡ºä¸€ä¸ªé›ªèŠ±å›¾ç»“æœã€‚

![é›ªèŠ±å›¾](./assets/é›ªèŠ±å›¾.png)

## é¢—ç²’åŒ–æ»¤é•œ
å¦‚æœå°†è¿™äº›å™ªç‚¹ä¸çº¹ç†å åŠ åœ¨ä¸€èµ·ï¼Œå°±ä¼šå¾—åˆ°ä¸€ç§è€ç›¸ç‰‡çš„æ»¤é•œçš„æ„Ÿè§‰ï½

å®Œæ•´ä»£ç å¦‚ä¸‹ï¼š
```glsl
#iChannel0"https://s2.loli.net/2023/09/10/QozT59R6KsYmb3q.jpg"

highp float random(vec2 co)
{
    highp float a=12.9898;
    highp float b=78.233;
    highp float c=43758.5453;
    highp float dt=dot(co.xy,vec2(a,b));
    highp float sn=mod(dt,3.14);
    return fract(sin(sn)*c);
}

void mainImage(out vec4 fragColor,in vec2 fragCoord){
    vec2 uv=fragCoord/iResolution.xy;
    
    float noise=random(uv);
    
    vec3 col=texture(iChannel0,uv).xyz;
    col+=noise*.2;
    fragColor=vec4(col,1.);
}
```

![å™ªç‚¹èƒŒæ™¯å›¾](./assets/å™ªç‚¹ç…§ç‰‡.png)

## å™ªå£°
å™ªå£°ä¹Ÿæ˜¯å®ç°â€œéšæœºâ€çš„ä¸€ç§æ‰‹æ®µï¼Œåªä¸è¿‡å®ƒèƒ½ä½¿å®ç°çš„æ•ˆæœæ›´ä½³åâ€œè‡ªç„¶â€ä¸€äº›ã€‚

å¹³æ—¶ç”Ÿæ´»ä¸­çœ‹åˆ°çš„ä¸€äº›äº‘é›¾ï¼Œå²©çŸ³ï¼Œç«ç„°ç­‰ï¼Œéƒ½æ˜¯å­˜åœ¨å™ªå£°çš„ã€‚

åŒæ ·ï¼Œå’Œç¼ºå°‘çš„å†…ç½®å‡½æ•°ä¸€æ ·ï¼Œ`GLSL`é‡ŒåŒæ ·ä¹Ÿæ²¡æœ‰å†…ç½®çš„å™ªå£°å‡½æ•°ã€‚ä½†æ˜¯`GitHub`ä¸Šï¼Œä¹Ÿæœ‰å°è£…å¥½çš„å™ªå£°å‡½æ•°[glsl-noise](https://github.com/hughsk/glsl-noise)ã€‚

è¿™é‡Œé€‰æ‹©å…¶ä¸­æ¯”è¾ƒå¸¸ç”¨çš„æŸæ—å™ªå£°`Perlin Noise`çš„`3d`ç‰ˆæœ¬ï¼Œå¯ä»¥æŸ¥çœ‹å®ƒæ˜¯ä»€ä¹ˆæ ·å­çš„ã€‚

```glsl
//
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x)
{
    return x-floor(x*(1./289.))*289.;
}

vec4 mod289(vec4 x)
{
    return x-floor(x*(1./289.))*289.;
}

vec4 permute(vec4 x)
{
    return mod289(((x*34.)+1.)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
    return 1.79284291400159-.85373472095314*r;
}

vec3 fade(vec3 t){
    return t*t*t*(t*(t*6.-15.)+10.);
}

// Classic Perlin noise
float cnoise(vec3 P)
{
    vec3 Pi0=floor(P);// Integer part for indexing
    vec3 Pi1=Pi0+vec3(1.);// Integer part + 1
    Pi0=mod289(Pi0);
    Pi1=mod289(Pi1);
    vec3 Pf0=fract(P);// Fractional part for interpolation
    vec3 Pf1=Pf0-vec3(1.);// Fractional part - 1.0
    vec4 ix=vec4(Pi0.x,Pi1.x,Pi0.x,Pi1.x);
    vec4 iy=vec4(Pi0.yy,Pi1.yy);
    vec4 iz0=Pi0.zzzz;
    vec4 iz1=Pi1.zzzz;

    vec4 ixy=permute(permute(ix)+iy);
    vec4 ixy0=permute(ixy+iz0);
    vec4 ixy1=permute(ixy+iz1);

    vec4 gx0=ixy0*(1./7.);
    vec4 gy0=fract(floor(gx0)*(1./7.))-.5;
    gx0=fract(gx0);
    vec4 gz0=vec4(.5)-abs(gx0)-abs(gy0);
    vec4 sz0=step(gz0,vec4(0.));
    gx0-=sz0*(step(0.,gx0)-.5);
    gy0-=sz0*(step(0.,gy0)-.5);

    vec4 gx1=ixy1*(1./7.);
    vec4 gy1=fract(floor(gx1)*(1./7.))-.5;
    gx1=fract(gx1);
    vec4 gz1=vec4(.5)-abs(gx1)-abs(gy1);
    vec4 sz1=step(gz1,vec4(0.));
    gx1-=sz1*(step(0.,gx1)-.5);
    gy1-=sz1*(step(0.,gy1)-.5);

    vec3 g000=vec3(gx0.x,gy0.x,gz0.x);
    vec3 g100=vec3(gx0.y,gy0.y,gz0.y);
    vec3 g010=vec3(gx0.z,gy0.z,gz0.z);
    vec3 g110=vec3(gx0.w,gy0.w,gz0.w);
    vec3 g001=vec3(gx1.x,gy1.x,gz1.x);
    vec3 g101=vec3(gx1.y,gy1.y,gz1.y);
    vec3 g011=vec3(gx1.z,gy1.z,gz1.z);
    vec3 g111=vec3(gx1.w,gy1.w,gz1.w);

    vec4 norm0=taylorInvSqrt(vec4(dot(g000,g000),dot(g010,g010),dot(g100,g100),dot(g110,g110)));
    g000*=norm0.x;
    g010*=norm0.y;
    g100*=norm0.z;
    g110*=norm0.w;
    vec4 norm1=taylorInvSqrt(vec4(dot(g001,g001),dot(g011,g011),dot(g101,g101),dot(g111,g111)));
    g001*=norm1.x;
    g011*=norm1.y;
    g101*=norm1.z;
    g111*=norm1.w;

    float n000=dot(g000,Pf0);
    float n100=dot(g100,vec3(Pf1.x,Pf0.yz));
    float n010=dot(g010,vec3(Pf0.x,Pf1.y,Pf0.z));
    float n110=dot(g110,vec3(Pf1.xy,Pf0.z));
    float n001=dot(g001,vec3(Pf0.xy,Pf1.z));
    float n101=dot(g101,vec3(Pf1.x,Pf0.y,Pf1.z));
    float n011=dot(g011,vec3(Pf0.x,Pf1.yz));
    float n111=dot(g111,Pf1);

    vec3 fade_xyz=fade(Pf0);
    vec4 n_z=mix(vec4(n000,n100,n010,n110),vec4(n001,n101,n011,n111),fade_xyz.z);
    vec2 n_yz=mix(n_z.xy,n_z.zw,fade_xyz.y);
    float n_xyz=mix(n_yz.x,n_yz.y,fade_xyz.x);
    return 2.2*n_xyz;
}
```
ä¼ å…¥åˆ°uvä¸­ï¼Œçœ‹çœ‹è¾“å‡ºğŸ‘€
```glsl
void mainImage(out vec4 fragColor,in vec2 fragCoord){
    vec2 uv=fragCoord/iResolution.xy;
    
    float noise=cnoise(vec3(uv*10.,0.));
    
    vec3 col=vec3(noise);
    fragColor=vec4(col,1.);
}
```
ç¼©æ”¾uvåæ ‡10å€ä¹‹åï¼Œå¯ä»¥çœ‹åˆ°å™ªå£°å›¾å¦‚ä¸‹â¬‡ï¸

![å™ªå£°å›¾](./assets/å™ªå£°å›¾.png)

## å™ªå£°è½¬åœº
ä¹‹å‰æœ‰å†™è¿‡é®ç½©è½¬åœºæ•ˆæœï¼Œç°åœ¨å¯ä»¥åŠ ä¸Šå™ªå£°åšå‡ºæ›´å¥½çš„æ•ˆæœã€‚åŠ å…¥ä¸¤å¼ çº¹ç†ã€‚
```glsl
#iChannel0 "https://s2.loli.net/2023/09/10/QozT59R6KsYmb3q.jpg"
#iChannel1 "https://s2.loli.net/2023/09/10/Jb8mIhZMBElPiuC.jpg"

```

å†™ä¸‹å¦‚ä¸‹çš„ä»£ç ï¼š
```glsl
vec4 getFromColor(vec2 uv){
    return texture(iChannel0,uv);
}

vec4 getToColor(vec2 uv){
    return texture(iChannel1,uv);
}

float sdfCircle(vec2 p,float r){
    return length(p)-r;
}

vec4 transition(vec2 uv){
    float progress=iMouse.x/iResolution.x;
    float ratio=iResolution.x/iResolution.y;
    
    vec2 p=uv;
    p-=.5;
    p.x*=ratio;
    
    float noise=cnoise(vec3(p*10.,0.));
    float pr=progress+noise*.1;
    
    float d=sdfCircle(p,pr*sqrt(2.4));
    float c=smoothstep(-.1,-.05,d);
    
    return mix(getFromColor(uv),getToColor(uv),1.-c);
}

void mainImage(out vec4 fragColor,in vec2 fragCoord){
    vec2 uv=fragCoord/iResolution.xy;
    
    vec4 col=transition(uv);
    
    fragColor=col;
}
```
å¯ä»¥å¾—åˆ°å¦‚ä¸‹çš„è½¬åœºæ•ˆæœï¼š
![å™ªå£°è¿‡æ¸¡](./assets/å™ªå£°è¿‡æ¸¡.png)

## FBM
`FBM`çš„å…¨ç§°æ˜¯`Fractal Brownian Motion`ï¼Œç¿»è¯‘ä¸º**åˆ†å½¢å¸ƒæœ—è¿åŠ¨**ï¼Œå®ƒæ˜¯å°†å¤šä¸ªå…·æœ‰ä¸åŒé¢‘ç‡å’ŒæŒ¯å¹…çš„å™ªå£°çš„ç»“æœå€¼ç›¸å åŠ è€Œäº§ç”Ÿç»“æœçš„ä¸€ç§éšæœºè¿‡ç¨‹ã€‚ä¸€èˆ¬ç”¨äºæ„é€ è‡ªç„¶ç•Œçš„äº‘å±‚ï¼Œå±±è„‰ï¼Œåœ°è²Œç­‰ä¸è§„åˆ™çš„å½¢ä½“ã€‚

ä¸Šæ–‡ä¸­ç”¨çš„æ˜¯æŸæ—å™ªå£°ï¼Œè¿™é‡ŒæŠŠå™ªå£°æ¢æˆæ›´åŠ è‡ªç„¶çš„`Simplex`å™ªå£°ï¼Œå®ƒçš„å…·ä½“ä»£ç å¦‚ä¸‹ï¼š
```glsl
//
// Description : Array and textureless GLSL 2D/3D/4D simplex
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v)
  {
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

// Permutations
  i = mod289(i);
  vec4 p = permute( permute( permute(
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients: 7x7 points over a square, mapped onto an octahedron.
// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                dot(p2,x2), dot(p3,x3) ) );
  }

#pragma glslify: export(snoise)
```
ç„¶åæ¥å®ç°`fbm`å‡½æ•°ï¼š
```glsl
            float fbm(vec3 p){
                float value=0.;
                float amplitude=1.;
                float frequency=1.;
                float lacunarity=2.;
                float persistance=.5;
                float scale=1.;
                int octaves=8;
                
                for(int i=0;i<octaves;i++){
                    float noiseVal=snoise(p*frequency*scale);
                    
                    value+=amplitude*noiseVal;
                    frequency*=lacunarity;
                    amplitude*=persistance;
                }
                
                return value;
            }
```
è¿™é‡Œå®šä¹‰äº†å‡ ä¸ªå˜é‡ï¼š`value`ä»£è¡¨ç»“æœå€¼ï¼Œ`amplitude`ä»£è¡¨æŒ¯å¹…ï¼Œ`frequency`ä»£è¡¨é¢‘ç‡ï¼Œ`lacunarity`ä»£è¡¨ç©ºéš™ï¼Œ`persistance`ä»£è¡¨æŒç»­åº¦ï¼Œ`scale`ä»£è¡¨ç¼©æ”¾ç¨‹åº¦ï¼Œ`octaves`ä»£è¡¨éŸ³åº¦ã€‚

ç„¶åé€šè¿‡`for`å¾ªç¯æ¥å åŠ å™ªå£°ï¼Œå åŠ çš„æ¬¡æ•°å°±æ˜¯éŸ³åº¦ï¼Œåœ¨å åŠ çš„åŒæ—¶å‡é«˜é¢‘ç‡ï¼Œé™ä½æŒ¯å¹…ã€‚

é€šè¿‡uvæ¥è¿›è¡Œè§‚å¯Ÿï¼š
```glsl
            void mainImage(out vec4 fragColor,in vec2 fragCoord){
                vec2 uv=fragCoord/iResolution.xy;
                float noise = fbm(vec3(uv,0.));
                vec3 col = vec3(noise);
                fragColor = vec4(col,1.);
                //fragColor=col;
            }
```
ä¼šå¾—åˆ°ä¸€ä¸ªäº‘å±‚ä¸€èˆ¬çš„å™ªå£°å›¾æ¡ˆ
![äº‘å±‚å™ªå£°](./assets/äº‘å±‚å™ªå£°.png)